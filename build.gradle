plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.7.20'
    id 'application'
    id("com.github.johnrengelman.shadow") version "7.1.2"
    id 'idea'
    id("io.github.patrick.remapper") version "1.3.0"
}

group = 'sk.ignissak'
def currentVersion = '1.0.0'

def target = 17
java {
    def javaVersion = JavaVersion.toVersion(target)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(target)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (target >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = target
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

project.setProperty('mainClassName', 'sk.ignissak.mobsurvival.Main')
shadowJar {
    archiveBaseName.set('mobsurvival')
    archiveClassifier.set('')
    archiveVersion.set(currentVersion)

    dependencies {
        exclude(dependency("org.jetbrains.kotlin:kotlin-stdlib"))
        exclude("kotlin")
    }
}

clean { delete "./build" }

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

task generateJavadocs(type: Javadoc) {
    source = sourceSets.main.allJava
    classpath = sourceSets.main.compileClasspath
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks {
    remap {
        version.set("1.19.2")
    }
}


repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

        // As of Gradle 5.1, you can limit this to only those
        // dependencies you expect from it
        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }
    maven {
        url "https://gitlab.com/api/v4/groups/craftmania/-/packages/maven"
        name "GitLab"
        credentials(HttpHeaderCredentials) {
            name = 'Private-Token'
            value = GITLAB_TOKEN
        }
        authentication {
            header(HttpHeaderAuthentication)
        }
    }
}

dependencies {
    compileOnly ('org.spigotmc:spigot-api:1.19.2-R0.1-SNAPSHOT')
    compileOnly ('org.spigotmc:spigot:1.19.2-R0.1-SNAPSHOT:remapped-mojang')
    compileOnly 'cz.craftmania.craftcore:craftcore:2.9'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}

build.dependsOn shadowJar

task compileAndRemap {
    dependsOn 'clean'
    dependsOn 'shadowJar'
    dependsOn 'remap'
    tasks.findByName('remap').mustRunAfter 'shadowJar'
}